// --------------------------------------------------------------------
// Parametric geometry of a 3-D axisymmetric cylinder.
//
// Author: Walter Dal'Maz Silva
// Date  : Jan 14 2025
// --------------------------------------------------------------------

General.BackgroundGradient = 0;
General.Color.Background = {200, 220, 240};
General.Color.Foreground = White;
General.Color.Text = White;
General.Color.Axes = White;
General.Color.SmallAxes = White;
General.Axes = 1;
General.SmallAxes = 1;
Geometry.OldNewReg = 0;

// --------------------------------------------------------------------
// Parameters
// --------------------------------------------------------------------

// Molten ingot radius:
radius = 0.10;

// Molten ingot height:
height = 0.05;

// Cells per meter on average:
rho = 500.0;

// --------------------------------------------------------------------
// Computed coordinates
// --------------------------------------------------------------------

x1 = 0.005;
y1 = 0.0;
x2 = radius;
y2 = height;

// --------------------------------------------------------------------
// Computed discretization
// --------------------------------------------------------------------

n1 = 1 + rho * (x2 - x1);
n2 = 1 + rho * (y2 - y1);

// --------------------------------------------------------------------
// Points
// --------------------------------------------------------------------

Point(1)  = {x1, y1, 0};
Point(2)  = {x2, y1, 0};
Point(3)  = {x2, y2, 0};
Point(4)  = {x1, y2, 0};

// --------------------------------------------------------------------
// Lines
// --------------------------------------------------------------------

Line(1) = {1, 2};
Line(2) = {2, 3};
Line(3) = {3, 4};
Line(4) = {4, 1};

// --------------------------------------------------------------------
// Loops and surfaces
// --------------------------------------------------------------------

Curve Loop(1) = {1, 2, 3, 4};
Plane Surface(1) = {1};

// --------------------------------------------------------------------
// Mesh structuring
// --------------------------------------------------------------------

Transfinite Curve{1, -3} = n1 Using Progression 0.99;
Transfinite Curve{2, -4} = n2;

Transfinite Surface {1};
Recombine Surface{1};

Rotate {{0, 1, 0}, {0, 0, 0}, -Pi/8} {
    Surface{1}; 
}
  
Extrude {{0, 1, 0}, {0, 0, 0}, Pi/4} {
    Surface{1}; Layers{5}; Recombine;
}

// --------------------------------------------------------------------
// Physics: boundary conditions
// --------------------------------------------------------------------

// Symmetry (hole):
Physical Surface(1) = {6};

// Radius:
Physical Surface(2) = {4};

// Bottom/top:
Physical Surface(3) = {3, 5};

// Symmetry (sides):
Physical Surface(4) = {1, 7};

// --------------------------------------------------------------------
// Physics: bodies
// --------------------------------------------------------------------

Physical Volume(1) = {1};

// --------------------------------------------------------------------
// Meshing
// --------------------------------------------------------------------

Mesh 3;

SetOrder 2;

Save "domain.msh";

// --------------------------------------------------------------------
// EOF
// --------------------------------------------------------------------